Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
                      |	СОКРЛП(ОСМИ_ЭлектроннаяКарта.Код) КАК СерийныйНомер,
                      |	БонусныеБаллы.ДисконтнаяКарта КАК Карта,
                      |	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
                      |	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию
                      |ПОМЕСТИТЬ втБонусныеБаллы
                      |ИЗ
                      |	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОСМИ_СоответствиеКарт КАК ОСМИ_СоответствиеКарт
                      |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОСМИ_ЭлектроннаяКарта КАК ОСМИ_ЭлектроннаяКарта
                      |			ПО ОСМИ_СоответствиеКарт.ЭлектроннаяКарта = ОСМИ_ЭлектроннаяКарта.Ссылка
                      |		ПО БонусныеБаллы.ДисконтнаяКарта = ОСМИ_СоответствиеКарт.ОбъектБазы
                      |ГДЕ
                      |	БонусныеБаллы.Период = &Дата
                      |	И БонусныеБаллы.БонуснаяПрограммаЛояльности = &БонуснаяПрограммаЛояльности
                      |
                      |СГРУППИРОВАТЬ ПО
                      |	БонусныеБаллы.ДисконтнаяКарта,
                      |	СОКРЛП(ОСМИ_ЭлектроннаяКарта.Код)
                      |;
                      |
                      |////////////////////////////////////////////////////////////////////////////////
                      |ВЫБРАТЬ
                      |	втБонусныеБаллы.СерийныйНомер КАК СерийныйНомер,
                      |	ВЫБОР
                      |		КОГДА СОКРЛП(ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """")) <> """"
                      |				И СОКРЛП(ЕСТЬNULL(ФИОФизЛицСрезПоследних.Имя, """")) <> """"
                      |			ТОГДА СОКРЛП(ФИОФизЛицСрезПоследних.Имя)
                      |		ИНАЧЕ СОКРЛП(ФизическиеЛица.Наименование)
                      |	КОНЕЦ КАК ИмяВладельца,
                      |	ЕСТЬNULL(втБонусныеБаллы.Начислено, 0) - ЕСТЬNULL(втБонусныеБаллы.КСписанию, 0) КАК Начислено
                      |ИЗ
                      |	втБонусныеБаллы КАК втБонусныеБаллы
                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИнформационныеКарты КАК ИнформационныеКарты
                      |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
                      |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних КАК ФИОФизЛицСрезПоследних
                      |				ПО ФизическиеЛица.Ссылка = ФИОФизЛицСрезПоследних.ФизЛицо
                      |			ПО ИнформационныеКарты.ВладелецКарты = ФизическиеЛица.Ссылка
                      |		ПО втБонусныеБаллы.Карта = ИнформационныеКарты.Ссылка
                      |ГДЕ
                      |	ЕСТЬNULL(втБонусныеБаллы.Начислено, 0) - ЕСТЬNULL(втБонусныеБаллы.КСписанию, 0) > 0");

Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДатаСеанса()));
Запрос.УстановитьПараметр("БонуснаяПрограммаЛояльности", Отборы.Получить(0).Значение);

Выборка = Запрос.Выполнить().Выбрать();
Пока Выборка.Следующий() Цикл
	МассивСерийныхНомеров = Новый Массив;
	МассивСерийныхНомеров.Добавить(Выборка.СерийныйНомер);
	ОСМИ_РаботаСAPI.ОтправитьПушСообщение(, МассивСерийныхНомеров,,, ?(Выборка.ИмяВладельца <> "", Выборка.ИмяВладельца + ", ", "") +
		"на вашу карту начислено " + Выборка.Начислено + " бонусов ❤️ Увидимся!");
КонецЦикла;