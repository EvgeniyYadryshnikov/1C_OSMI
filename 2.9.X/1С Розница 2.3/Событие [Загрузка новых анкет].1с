//Параметры:
//	1. Вид дисконтных карт
//	2. Наименование регистрационной группы
//	3. Префикс ШК новых карты (два символа). ШК - 11 символов

ВидДисконтнойКарты = Отборы.Получить(1).Значение;

МассивКарт = ОСМИ_РаботаСAPI.ПолучитьСписокЗарегистрированныхКарт(Отборы.Получить(0).Значение).Ответ.registrations;
МассивНаУдаление = Новый Массив;
	
Для каждого Карта из МассивКарт Цикл
				
	Попытка
			
		НачатьТранзакцию();
			
		КодКарты = СокрЛП(Карта.serialNo);
		
		Имя = СокрЛП(СтрЗаменить(Карта.Имя, "-empty-", ""));
		
		Попытка
			ДатаРождения = Дата(Карта.Дата_рождения + " 00:00:00");
		Исключение
			ДатаРождения = Дата(1, 1, 1);
		КонецПопытки;
				
		Телефон = СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрЗаменить(Карта.Телефон, " ", ""), ")", ""), "(", ""), "-", ""), "-empty-", "");
		Почта = СокрЛП(СтрЗаменить(Карта.Почта, "-empty-", ""));
		
		флНовоеФизЛицо = Истина;
		
		Если Телефон <> "" Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			                      |	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК Ссылка
			                      |ИЗ
			                      |	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
			                      |ГДЕ
			                      |	ФизическиеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
			                      |	И ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонФизическогоЛица)
			                      |	И (ФизическиеЛицаКонтактнаяИнформация.НомерТелефона = &НомерТелефона
			                      |			ИЛИ ФизическиеЛицаКонтактнаяИнформация.НомерТелефона = &НомерТелефона7
			                      |			ИЛИ ФизическиеЛицаКонтактнаяИнформация.НомерТелефона = &НомерТелефона8)
			                      |	И НЕ ФизическиеЛицаКонтактнаяИнформация.Ссылка.ПометкаУдаления");
			
			Запрос.УстановитьПараметр("НомерТелефона", Телефон);
			Запрос.УстановитьПараметр("НомерТелефона7", "7" + Прав(Телефон, 10));
			Запрос.УстановитьПараметр("НомерТелефона8", "8" + Прав(Телефон, 10));
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ФизЛицо = Выборка.Ссылка.ПолучитьОбъект();
				флНовоеФизЛицо = Ложь;
			КонецЕсли;
			
		Иначе
			ВызватьИсключение "В новой дисконтной карте не задан номер телефона!";
		КонецЕсли;
		
		Если флНовоеФизЛицо Тогда
			
			ФизЛицо = Справочники.ФизическиеЛица.СоздатьЭлемент();
			ФизЛицо.Наименование = Имя;
			ФизЛицо.ДатаРождения = ДатаРождения;
			
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(ФизЛицо, Телефон,
				Справочники.ВидыКонтактнойИнформации.ТелефонФизическогоЛица, ТекущаяДата(), Истина);
				
		КонецЕсли;			
		
		//Почта
		Если Почта <> "" Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(ФизЛицо, Почта,
				Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица, ТекущаяДата(), Истина);
		КонецЕсли;
		
		ФизЛицо.Записать();
		ФизЛицо = ФизЛицо.Ссылка;
		
		НаборЗаписейФИО = РегистрыСведений.ФИОФизЛиц.СоздатьНаборЗаписей();
		НаборЗаписейФИО.Отбор.ФизЛицо.Установить(ФизЛицо);
	
		ЗаписьФИО = НаборЗаписейФИО.Добавить();
		ЗаписьФИО.Имя			= Имя;
		ЗаписьФИО.Активность	= Истина;
		ЗаписьФИО.Период		= ТекущаяДата();
		ЗаписьФИО.ФизЛицо		= ФизЛицо;
		НаборЗаписейФИО.Записать(Истина);
		
		//Карта лояльности
		
		НовыйКод = Отборы.Получить(2).Значение + Прав("000000000" + КодКарты, 9);
			
		//Информационная карта
		//Если клиент действующий - попытаемся найти действующую информационную карту
		
		Карта1С = Неопределено;
		
		Если НЕ флНовоеФизЛицо Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ
			                      |	ИнформационныеКарты.Ссылка КАК Карта
			                      |ИЗ
			                      |	Справочник.ИнформационныеКарты КАК ИнформационныеКарты
			                      |ГДЕ
			                      |	ИнформационныеКарты.ВладелецКарты = &ФизЛицо
			                      |	И ИнформационныеКарты.ВидДисконтнойКарты = &ВидДисконтнойКарты
			                      |	И ИнформационныеКарты.ВидКарты = ЗНАЧЕНИЕ(Перечисление.ВидыИнформационныхКарт.Штриховая)
			                      |	И ИнформационныеКарты.ТипКарты = ЗНАЧЕНИЕ(Перечисление.ТипыИнформационныхКарт.Дисконтная)
			                      |	И НЕ ИнформационныеКарты.ПометкаУдаления");
			
			Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
			Запрос.УстановитьПараметр("ВидДисконтнойКарты", ВидДисконтнойКарты);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Карта1С = Выборка.Карта.ПолучитьОбъект();
			КонецЕсли;
			
		КонецЕсли;
		
		Если Карта1С = Неопределено Тогда
			
			Карта1С = Справочники.ИнформационныеКарты.СоздатьЭлемент();
			Карта1С.Наименование = НовыйКод + " (" + Имя + ")";
			Карта1С.ВладелецКарты = ФизЛицо;
			Карта1С.ТипКарты = Перечисления.ТипыИнформационныхКарт.Дисконтная;
			Карта1С.ВидДисконтнойКарты = ВидДисконтнойКарты;
			Карта1С.ВидКарты = Перечисления.ВидыИнформационныхКарт.Штриховая;
			Карта1С.КодКарты = НовыйКод;
			
		КонецЕсли;
	
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Карта1С, Телефон,
			Справочники.ВидыКонтактнойИнформации.ТелефонИнформационнойКарты, ТекущаяДата(), Истина);

		Если Почта <> "" Тогда
			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Карта1С, Почта,
				Справочники.ВидыКонтактнойИнформации.EmailИнформационнойКарты, ТекущаяДата(), Истина);
		КонецЕсли;	

		Карта1С.Записать();
		Карта1С = Карта1С.Ссылка;
	
		НаборЗаписейШтрихкодов = РегистрыСведений.Штрихкоды.СоздатьНаборЗаписей();
		НаборЗаписейШтрихкодов.Отбор.Владелец.Установить(Карта1С);
	
		ЗаписьШтрихкода = НаборЗаписейШтрихкодов.Добавить();
		ЗаписьШтрихкода.Владелец     = Карта1С;
		ЗаписьШтрихкода.ТипШтрихкода = ПланыВидовХарактеристик.ТипыШтрихкодов.CODE39;
		ЗаписьШтрихкода.Штрихкод     = НовыйКод;
		
		Если НЕ НаборЗаписейШтрихкодов.ПроверитьЗаполнение() Тогда
			ВызватьИсключение "Сформирован некорректный штрихкод";
		Иначе
			НаборЗаписейШтрихкодов.Записать();
		КонецЕсли;
			
		//Электронная карта
		КартаОСМИ = Справочники.ОСМИ_ЭлектроннаяКарта.СоздатьЭлемент();
		КартаОСМИ.Код = КодКарты;
		КартаОСМИ.Наименование = "(" + КодКарты + ") " + Имя;
		КартаОСМИ.Шаблон = Константы.ОСМИ_ШаблонПоУмолчанию.Получить();
		КартаОСМИ.ОбменДанными.Загрузка = Истина;
		КартаОСМИ.Записать();
			
		РегОСМИ = РегистрыСведений.ОСМИ_СоответствиеКарт.СоздатьМенеджерЗаписи();
		РегОСМИ.ОбъектБазы = Карта1С;
		РегОСМИ.ЭлектроннаяКарта = КартаОСМИ.Ссылка;
		РегОСМИ.Записать();
		
		//Заполним поля
		КартаОСМИ.УстановитьЗначенияПолей();
		КартаОСМИ.ОбновитьЗначенияССервера();
		
		//Еще раз запишем
		КартаОСМИ.Записать();
		
		Результат.Добавить(Карта1С);
		ЗафиксироватьТранзакцию();
	
		МассивНаУдаление.Добавить(КодКарты);			
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации("ОСМИ.Ошибка",УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки());
	КонецПопытки;
		
КонецЦикла;
	
Если МассивНаУдаление.Количество() > 0 Тогда 
	ОСМИ_РаботаСAPI.УдалитьРегиcтрацию(МассивНаУдаление);
КонецЕсли;
